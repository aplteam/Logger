 R←Test_02(stopFlag batchFlag);path;myLogger;list;⎕TRAP
⍝ Try to write strings/vector-of-strings/char matrix. Checks also a day switch.
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 path←GetFileRoot,'LogFiles'
 :If #.FilesAndDirs.IsDir path
     list←↑#.FilesAndDirs.Dir path,'/*.log'
     #.FilesAndDirs.DeleteFile list
 :EndIf

 myLogger←⎕NEW ##.Logger(path'ANSI' 'DATE' 1(2100 11 6 23 59 55)#)
 →GoToTidyUp 0≠myLogger.errorCounter
 myLogger.printToSession←1
 myLogger.Log'this is my first entry!'
 myLogger.Log'a' '...number...' '...of...' '...nested...' '...entries' '' 'By!'
 myLogger.timestamp←2100 11 7 0 0 0
 myLogger.Log'next day!'
 →GoToTidyUp 0≠myLogger.errorCounter

 list←↑#.FilesAndDirs.Dir path,'\'
 list←(1+⍴path)↓¨list
 list←{⍵↓⍨-1+'.'⍳⍨⌽⍵}¨list
 →GoToTidyUp~(⊂list)∊('21001106' '21001107')('21001107' '21001106')
 R←∆OK

∆TidyUp:
 :Trap 0
     myLogger.Close
 :EndTrap
 :Trap 0
     list←↑#.FilesAndDirs.Dir path,'/*.log'
     #.FilesAndDirs.DeleteFile list
 :EndTrap
