 R←Test_06(stopFlag batchFlag);path;myLogger;list;buffer;noOfThreads;⎕TRAP
⍝ Try to write to the log files from a number of threads started concurrently.
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 path←GetFileRoot,'LogFiles'
 :If #.FilesAndDirs.IsDir path
     →FailsIf 0≠0⊃#.FilesAndDirs.RmDir path   ⍝ Could not remove the directory "path" is pointing too?!'
 :EndIf

 myLogger←⎕NEW ##.Logger(path'ANSI' 'DATE' 1(2100 11 6)#)
 →FailsIf 0≠myLogger.errorCounter
 myLogger.printToSession←0
 noOfThreads←1000
 {myLogger.Log'hello universe in a thread! (',(⍕⍵),')'}&¨1+⍳noOfThreads
 :Repeat
     ⎕DL 0.01
 :Until 1=⍴⎕TNUMS
 →FailsIf 0≠myLogger.errorCounter
 myLogger.Close

 :If #.FilesAndDirs.IsDir path
     list←↑(#.FilesAndDirs.Dir path,'/*.*')
     buffer←#.APLTreeUtils.ReadUtf8File 0⊃list
     buffer←(0<↑¨⍴¨buffer)/buffer
     buffer←⊃1↓¯1↓buffer ⍝ Drop first and last (open and close notification)
     buffer←(∨/buffer∊'()')⌿buffer  ⍝ remove all lines without thread numbers
     buffer←{⍵/⍨{⍵∨≠\⍵}⍵∊'()'}¨↓buffer
     buffer←⊃{⍎⍵{⍵\⍵/⍺}~⍵∊'()'}¨buffer
     buffer←buffer[⍋buffer[;0];]
     →FailsIf(⍴buffer[;0])≢⍴∪buffer[;0]
     →FailsIf noOfThreads≠⍴buffer[;1]
     #.FilesAndDirs.DeleteFile list
 :EndIf

 R←∆OK
