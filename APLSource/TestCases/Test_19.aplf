 R←Test_19(stopFlag batchFlag);path;isUnicode;list;parmSpace;msg;myLogger;buffer;⎕TRAP
⍝ This test case is dealing with setting the "filename" to a certain type.
⍝ This is particularly important when the timestamp an application is running under
⍝ is falsified because it's a test.
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 path←'LogFiles' ⍝ relative!
 isUnicode←80=⎕DR' '
 :If FilesAndDirs.IsDir path
     list←⊃FilesAndDirs.Dir path,'/*.log'
     FilesAndDirs.DeleteFile list
 :EndIf

 parmSpace←##.Logger.CreatePropertySpace
 parmSpace.debug←1
 parmSpace.path←path
 parmSpace.printToSession←1
 parmSpace.active←1
 parmSpace.filename←'20000102'
 parmSpace.autoReOpen←0

 myLogger←⎕NEW ##.Logger(,⊂parmSpace)
 →T.FailsIf 0≠myLogger.errorCounter

 msg←'There is no problem'
 myLogger.Log msg

 →T.FailsIf 0≠myLogger.errorCounter

 buffer←⊃FilesAndDirs.NGET myLogger.FullFilename 1
 →T.FailsIf 2≠⍴buffer

 myLogger.Close
 list←⊃FilesAndDirs.Dir path,'/*.log'
 FilesAndDirs.DeleteFile list

 R←T._OK
